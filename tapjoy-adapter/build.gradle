apply plugin: 'com.android.library'
apply from: "$rootProject.projectDir/AppodealSDK.gradle"

android {
    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly project(path: ':appodealX')
    compileOnly fileTree(dir: "$rootProject.projectDir/SDKs/networks", include: ['*.jar', '*/*.jar'])

    testCompileOnly project(path: ':appodealX')
    testCompileOnly fileTree(dir: "$rootProject.projectDir/SDKs/networks", include: ['*.jar', '*/*.jar'])

    testImplementation files(project(':SDK').packageUnitTests.testJarLocation)
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

static def getJarFileName() {
    return "appodealX-tapjoy-adapter-0.0.1.jar"
}

task deleteJar(dependsOn: 'bundleRelease', group: 'appodealx') {
    doFirst {
        new File(rootDir, "$rootProject.projectDir/SDKs/libs-internal/"+getJarFileName()).delete()
    }
}

task copyToInternalJar(dependsOn: deleteJar, type: Copy, group: 'appodealx') {
    from('build/intermediates/packaged-classes/release')
    into("$rootProject.projectDir/SDKs/libs-internal")
    include('classes.jar')
    rename ('classes.jar', getJarFileName())
}
